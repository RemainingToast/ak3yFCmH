buildscript {
    repositories {
        gradlePluginPortal()
        maven {
            name = "Fabric"
            url = "https://maven.fabricmc.net/"
        }
    }
    dependencies {
        classpath "net.fabricmc:fabric-loom:${project.loomVersion}"
    }
}

apply plugin: "fabric-loom"

version = parent.version.toString().replaceAll("mcversion", project.minecraft19fabric)
sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17

processResources {
    inputs.property "version", project.version
    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft19fabric}"
    mappings "net.fabricmc:yarn:${project.mappings19fabric}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader19fabric}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.api19fabric}"
    implementation "com.lukflug:panelstudio-mc16-fabric:0.2.0"
    implementation project(":toastclient-api")
}

shadowJar {
    getDestinationDirectory().set(file("$rootDir/out"))

    final String archiveName = parent.name + '-' + project.version + '.jar'
    getArchiveFileName().set(archiveName)

    dependencies {
        include(project(':toastclient-api'))
        include(dependency('com.lukflug:.*'))
    }
}

build {
    dependsOn(shadowJar)
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}

