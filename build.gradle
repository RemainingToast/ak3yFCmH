plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'org.ajoberstar.grgit' version '4.1.1'
}

ext {
    branch = grgit.getBranch().current.name
    git_version = grgit.head().abbreviatedId
}

allprojects {
    group 'me.remainingtoast'
    version "mcversion-$branch+$git_version"
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'com.github.johnrengelman.shadow'

    dependencies {
        compileOnly 'org.projectlombok:lombok:1.18.24'
        annotationProcessor 'org.projectlombok:lombok:1.18.24'
        implementation 'com.github.therealbush:eventbus:1.0.2'
        implementation 'com.lukflug:panelstudio:0.2.1'
    }

    repositories {
        mavenCentral()

        maven {
            name = 'jitpack'
            url = 'https://jitpack.io'
        }

        maven {
            name = 'lukflug'
            url = 'https://lukflug.github.io/maven/'
        }

        maven {
            name = 'forge'
            url = 'https://maven.minecraftforge.net/'
        }

        maven {
            name = 'spongepowered-repo'
            url = 'https://repo.spongepowered.org/repository/maven-public/'
        }
    }

    build {
        dependsOn(shadowJar)
    }
}

build.doFirst {
    delete(file("$rootDir/out"))
}