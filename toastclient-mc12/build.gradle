buildscript {
	repositories {
		gradlePluginPortal()
		maven {
			name = "forge"
			url = "https://maven.minecraftforge.net/"
		}
	}
	dependencies {
		classpath "net.minecraftforge.gradle:ForgeGradle:${project.forgeGradle}"
	}
}

apply plugin: "net.minecraftforge.gradle"

version = parent.version.toString().replaceAll("mcversion", "1.12.2")
sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

processResources {
	duplicatesStrategy = DuplicatesStrategy.INCLUDE
	inputs.property "version", version
	from(sourceSets.main.resources.srcDirs) {
		include "mcmod.info"
		expand "version": version, "mcversion": "1.12.2"
	}
	from(sourceSets.main.resources.srcDirs) {
		exclude "mcmod.info"
	}
}

dependencies {
	minecraft "net.minecraftforge:forge:${project.minecraft12}"
	implementation project(':toastclient-api')
	implementation("org.spongepowered:mixin:0.7.11-SNAPSHOT") {
		exclude module: 'commons-io'
		exclude module: 'gson'
		exclude module: 'guava'
		exclude module: 'launchwrapper'
		exclude module: 'log4j-core'
	}
	implementation "com.lukflug:panelstudio-mc12:0.2.0"
}

minecraft {
	mappings("stable", project.mappings12)
}

shadowJar {
	getDestinationDirectory().set(file("$rootDir/out"))

	final String archiveName = parent.name + version + '.jar'
	getArchiveFileName().set(archiveName)

	dependencies {
		include(project(':toastclient-api'))
		include(dependency('com.lukflug:.*'))
		include(dependency('org.spongepowered:.*'))
	}
}

build {
	dependsOn(shadowJar)
}
